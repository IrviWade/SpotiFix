import "module:std" as std
import "module:spotube_plugin" as spotube

import { SpotifyGqlApi } from '../../dependencies/hetu_spotify_gql_client/lib/assets/hetu/spotify_gql_api_client.ht'
import { Converters } from '../converter/converter.ht'
import { SpotifyAuthEndpoint } from "./auth.ht"

var Timezone = spotube.Timezone
var Future = std.Future
var Duration = std.Duration

class BrowseEndpoint {
  var client: SpotifyGqlApi
  var auth: SpotifyAuthEndpoint

  construct (this.client, this.auth)

  fun _spTCookieFromCredentials(creds: Map) -> string {
    if (creds == null) {
      return null
    }
    var cookies = creds["cookies"]
    if (cookies == null) {
      return null
    }

    var spTCookies = cookies.where((c) => c["name"] == "sp_t").toList()

    if (spTCookies.isEmpty) {
      return null
    }

    return spTCookies.elementAt(0)["value"]
  }

  fun _executeWithAuth(run: (spTCookie: string) -> Future) {
    // Check if we have valid credentials
    if (auth.credentials != null && auth.credentials["accessToken"] != null) {
      var spTCookie = _spTCookieFromCredentials(auth.credentials)
      if (spTCookie != null) {
        return run(spTCookie)
      }
    }

    // Trigger login flow and wait briefly for credentials
    auth.authenticate()

    return _waitForCredentials(25).then((spTCookie){
      if (spTCookie != null) {
        return run(spTCookie)
      }

      // Still no credentials: return empty list to avoid crash
      return {
        limit: 20,
        nextOffset: null,
        hasMore: false,
        total: 0,
        items: []
      }.toJson()
    })
  }

  fun _waitForCredentials(maxTries: int) -> Future {
    var attempts = 0

    fun poll(resolve){
      attempts += 1
      if (auth.credentials != null && auth.credentials["accessToken"] != null) {
        var spTCookie = _spTCookieFromCredentials(auth.credentials)
        if (spTCookie != null) {
          return resolve(spTCookie)
        }
      }
      if (attempts >= maxTries) {
        return resolve(null)
      }
      std.Future.delayed(Duration(milliseconds: 200), (){ poll(resolve) })
    }

    return Future((resolve){ poll(resolve) })
  }

  fun sections({offset: int, limit: int}) {
    return _executeWithAuth((spTCookie){
      return Timezone.getLocalTimeZone().then((timeZone){
        return client.browse.home(
          timeZone: timeZone,
          spTCookie: spTCookie,
          limit: limit,
        ).then((sections){
          return {
            limit: limit ?? 20,
            nextOffset: null,
            hasMore: false,
            total: sections.length,
            items: sections.map((section) {            
              var playlists = section["items"].where((item) => item["objectType"] == "Playlist").toList()
              var albums = section["items"].where((item) => item["objectType"] == "Album").toList()
              var artists = section["items"].where((item) => item["objectType"] == "Artist").toList()
              
              return {
                id: section["id"],
                title: section["title"],
                externalUri: section["external_urls"]["spotify"] ?? "https://open.spotify.com/section/${section["id"]}",
                browseMore: true,
                items: [
                  ...Converters.simplePlaylistsFromLibraryV3(playlists),
                  ...Converters.simpleAlbums(albums),
                  ...Converters.fullArtists(artists),
                ],
              }.toJson()
            }).toList()
          }.toJson()
        })
      })
    })
  }

  fun sectionItems(id: string, {offset: int, limit: int}) {
    return _executeWithAuth((spTCookie){
      return Timezone.getLocalTimeZone().then((timeZone) {
        return client.browse.homeSection(
          id, 
          timeZone: timeZone,
          spTCookie: spTCookie,
          offset: offset,
          limit: limit,
        ).then((section){
          return Converters.paginated(
            section,
            (items) {
              var playlists = section["items"].where((item) => item["objectType"] == "Playlist").toList()
              var albums = section["items"].where((item) => item["objectType"] == "Album").toList()
              var artists = section["items"].where((item) => item["objectType"] == "Artist").toList()

              return [
                  ...Converters.simplePlaylistsFromLibraryV3(playlists),
                  ...Converters.simpleAlbums(albums),
                  ...Converters.fullArtists(artists),
                ]
            }
          )
        })
      })
    })
  }
}

export { BrowseEndpoint }