import "module:std" as std
import "module:spotube_plugin" as spotube

import { SpotifyGqlApi } from '../../dependencies/hetu_spotify_gql_client/lib/assets/hetu/spotify_gql_api_client.ht'
import { Converters } from '../converter/converter.ht'
import { SpotifyAuthEndpoint } from "./auth.ht"

var Timezone = spotube.Timezone

class BrowseEndpoint {
  var client: SpotifyGqlApi
  var auth: SpotifyAuthEndpoint
  var _authenticating: bool
  var _authPromise

  construct (this.client, this.auth){
    _authenticating = false
    _authPromise = null
  }

  fun _spTCookieFromCredentials(creds: Map) -> string {
    var cookies = creds?["cookies"]
    if (cookies == null) {
      throw { message: "Missing cookies. Please log in again." }
    }

    var spTCookies = cookies.where((c) => c["name"] == "sp_t").toList()

    if (spTCookies.isEmpty) {
      throw { message: "Missing sp_t cookie. Please log in via the web player." }
    }

    return spTCookies.elementAt(0)["value"]
  }

  fun _withValidCredentials(run: (spTCookie: string) -> Future) {
    if (auth.isAuthenticated()) {
      return run(_spTCookieFromCredentials(auth.credentials))
    }

    // Avoid opening multiple auth windows at once.
    if (_authenticating && _authPromise != null) {
      return _authPromise.then((_){
        if (!auth.isAuthenticated()) {
          throw { message: "Authentication required. Please log in." }
        }

        return run(_spTCookieFromCredentials(auth.credentials))
      })
    }

    _authenticating = true
    _authPromise = auth.authenticate()

    return _authPromise.then((_){
      _authenticating = false
      _authPromise = null

      if (!auth.isAuthenticated()) {
        throw { message: "Authentication required. Please log in." }
      }

      return run(_spTCookieFromCredentials(auth.credentials))
    })
  }

  fun sections({offset: int, limit: int}) {
    return _withValidCredentials((spTCookie){
      return Timezone.getLocalTimeZone().then((timeZone){
        return client.browse.home(
          timeZone: timeZone,
          spTCookie: spTCookie,
          limit: limit,
        ).then((sections){
          return {
            limit: limit ?? 20,
            nextOffset: null,
            hasMore: false,
            total: sections.length,
            items: sections.map((section) {            
              var playlists = section["items"].where((item) => item["objectType"] == "Playlist").toList()
              var albums = section["items"].where((item) => item["objectType"] == "Album").toList()
              var artists = section["items"].where((item) => item["objectType"] == "Artist").toList()
              
              return {
                id: section["id"],
                title: section["title"],
                externalUri: section["external_urls"]["spotify"] ?? "https://open.spotify.com/section/${section["id"]}",
                browseMore: true,
                items: [
                  ...Converters.simplePlaylistsFromLibraryV3(playlists),
                  ...Converters.simpleAlbums(albums),
                  ...Converters.fullArtists(artists),
                ],
              }.toJson()
            }).toList()
          }.toJson()
        })
      })
    })
  }

  fun sectionItems(id: string, {offset: int, limit: int}) {
    return _withValidCredentials((spTCookie){
      return Timezone.getLocalTimeZone().then((timeZone) {
        return client.browse.homeSection(
          id, 
          timeZone: timeZone,
          spTCookie: spTCookie,
          offset: offset,
          limit: limit,
        ).then((section){
          return Converters.paginated(
            section,
            (items) {
              var playlists = section["items"].where((item) => item["objectType"] == "Playlist").toList()
              var albums = section["items"].where((item) => item["objectType"] == "Album").toList()
              var artists = section["items"].where((item) => item["objectType"] == "Artist").toList()

              return [
                  ...Converters.simplePlaylistsFromLibraryV3(playlists),
                  ...Converters.simpleAlbums(albums),
                  ...Converters.fullArtists(artists),
                ]
            }
          )
        })
      })
    })
  }
}